Drawson {
  Start = StatementList

  StatementList = Statement*
      
  Statement 
    = WhileLoop
    | ForLoop
    | ClearStatement
    | SetStatement
    | DrawStatement
    | FillStatement
    | DefinitionStatement
    | AssignmentStatement
    | DefinitionWithAssignmentStatement
    | Comment

  WhileLoop = whileKeyword "(" ConditionalStatement ")" LoopBody
  
  whileKeyword = "while"

  ForLoop = forKeyword identifier inKeyword ArithmeticStatement ArithmeticStatement LoopBody

  forKeyword = "for"
  inKeyword = "in"

  LoopBody = "{" StatementList "}"

  DefinitionStatement = variableKeyword identifier ";"

  AssignmentStatement = identifier "=" ArithmeticStatement ";"

  DefinitionWithAssignmentStatement = variableKeyword identifier "=" ArithmeticStatement ";"

  variableKeyword = "let"

  ClearStatement = "clear" ";"

  SetStatement = "set" SetDefinition ";" 

  SetDefinition 
    = "color" colorValue
    | "paint" colorValue
    | "thickness" ArithmeticStatement
      
  DrawStatement = drawKeyword ObjectDefinition ";"

  drawKeyword = "draw"

  FillStatement = fillKeyword FillableObjectDefinition ";"

  fillKeyword = "fill"

  ObjectDefinition
    = FillableObjectDefinition
    | lineObjectKeyword ArithmeticStatement ArithmeticStatement ArithmeticStatement ArithmeticStatement -- line_definition
  
  lineObjectKeyword = "line"

  FillableObjectDefinition
    = circleObjectKeyword ArithmeticStatement ArithmeticStatement ArithmeticStatement -- circle_definition
    | rectObjectKeyword ArithmeticStatement ArithmeticStatement ArithmeticStatement ArithmeticStatement -- rect_definition

  circleObjectKeyword = "circle"
  rectObjectKeyword = "rect"
  
  ConditionalStatement 
    = "!" ConditionalStatement -- negation
    | ConditionalStatement logicalOperator ConditionalStatement -- operator_join
    | "(" ConditionalStatement ")" -- contidion_parentheses
    | ArithmeticStatement relationalOperator ArithmeticStatement -- arithmetic_condition

  ArithmeticStatement
    = AddStatement

  AddStatement
    = AddStatement "+" MulStatement -- plus
    | AddStatement "-" MulStatement -- minus
    | MulStatement

  MulStatement
    = MulStatement "*" PriStatement -- times
    | MulStatement "/" PriStatement -- divide
    | PriStatement

  PriStatement
    = "(" ArithmeticStatement ")" -- paren
    | "+" PriStatement -- pos
    | "-" PriStatement -- neg
    | identifier -- var
    | integer

  Comment = commentStart notEndOfComment* commentEnd
  commentStart = "/*"
  notEndOfComment= ~commentEnd any
  commentEnd = "*/"

  logicalOperator 
    = "&&"
    | "||"

  relationalOperator 
    = "=="
    | "!="
    | ">"
    | "<"
    | ">="
    | "<="

  identifier = ~drawKeyword ~fillKeyword ~circleObjectKeyword ~rectObjectKeyword ~lineObjectKeyword letter alnum*

  colorValue = "#" alnum alnum alnum alnum alnum alnum

  integer
    = nonZeroDigit digit* -- number
    | "0" -- zero

  nonZeroDigit = ~"0" digit
}